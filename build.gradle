buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.40.1"
        classpath "app.cash.molecule:molecule-gradle-plugin:0.1.0"
    }
}

plugins {
    id "com.android.application" version libs.versions.agp apply true
    id "com.android.library" version libs.versions.agp apply false
}

ext.configureBase = { project ->
    delegate = project
    apply plugin: "org.jetbrains.kotlin.android"
    apply plugin: "kotlin-kapt"
    group = GROUP
    version = VERSION_NAME
    android {
        compileSdkVersion 31
        defaultConfig {
            minSdkVersion 26
            targetSdkVersion 31
        }
        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += [
                "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xopt-in=kotlinx.coroutines.FlowPreview",
                "-Xopt-in=androidx.compose.animation.ExperimentalAnimationApi",
            ]
        }
        lintOptions {
            lintConfig rootProject.file("lint.xml")
            abortOnError false
        }
        testOptions {
            unitTests {
                includeAndroidResources = true
            }
        }
    }
    dependencies {
        api libs.bundles.kotlin
    }
    kapt {
        correctErrorTypes = true
    }
}

ext.configureHilt = { project ->
    delegate = project
    apply plugin: "dagger.hilt.android.plugin"
    dependencies {
        implementation libs.hilt.android
        kapt libs.hilt.android.compiler
        testImplementation libs.hilt.android.testing
        kaptTest libs.hilt.android.compiler
    }
    kapt {
        arguments {
            arg("dagger.hilt.disableModulesHaveInstallInCheck", "true")
        }
    }
}

ext.configureCompose = { project ->
    delegate = project
    apply plugin: "app.cash.molecule"
    android {
        buildFeatures {
            compose true
        }
        composeOptions {
            kotlinCompilerExtensionVersion "1.0.5"
        }
    }
    dependencies {
        implementation libs.bundles.compose
    }
}

project.configureBase(this)
project.configureHilt(this)
project.configureCompose(this)

android {
    defaultConfig {
        applicationId "com.github.bentilbrook.barnacle"
        versionName "0.0.1"
        versionCode 1
        vectorDrawables.useSupportLibrary = true
        proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
        }
    }
}

dependencies {
    implementation projects.core
    implementation projects.repolist
    implementation libs.google.material
    implementation libs.moshi
    implementation libs.bundles.core
    implementation libs.bundles.compose
    testImplementation projects.testing
}
